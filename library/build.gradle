apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.android.library'

def versionMajor = 4
def versionMinor = 99
def versionPatch = 1

def libraryGroupId = 'com.devbrackets.android'
def libraryBaseName = 'exomedia'
def libraryVersion = "${versionMajor}.${versionMinor}.${versionPatch}"


group libraryGroupId
version = libraryVersion

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

repositories {
    maven {
        url("https://ampme.bintray.com/maven")
        credentials {
            username System.properties['bintray.user']
            password System.properties['bintray.key']
        }
    }
}

dependencies {
    // Android
    compile 'com.android.support:appcompat-v7:25.3.1'

    // ExoPlayer
    compile('com.google.android.exoplayer:library-core:r2.99.0')
    compile('com.google.android.exoplayer:exoplayer-dash:r2.4.0') {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-core'
    }
    compile('com.google.android.exoplayer:exoplayer-hls:r2.4.0') {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-core'
    }
    compile('com.google.android.exoplayer:exoplayer-smoothstreaming:r2.4.0') {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-core'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
}

bintray {
    user = System.properties['bintray.user']
    key = System.properties['bintray.key']
    pkg {
        name = 'exomedia'
        repo = 'maven'
        userOrg = 'ampme'
    }
    publish = true
    configurations = ['archives']
}

afterEvaluate {
    project.bintray {
        publish = true
        pkg {
            userOrg = 'ampme'
        }
    }
}

install {
    repositories {
        mavenInstaller {
            pom {
                project {
                    packaging 'aar'
                }
            }
        }
    }
}
