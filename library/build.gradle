apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.novoda.bintray-release'

def versionMajor = 4
def versionMinor = 1
def versionPatch = 99

def libraryGroupId = 'com.devbrackets.android'
def libraryBaseName = 'exomedia'
def libraryVersion = "${versionMajor}.${versionMinor}.${versionPatch}"

repositories {
    maven {
        url("https://ampme.bintray.com/maven")
        credentials {
            username System.properties['bintray.user']
            password System.properties['bintray.key']
        }
    }
}

dependencies {
    compile "com.android.support:support-media-compat:$rootProject.ext.supportLibVersion"
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVersion"

    // ExoPlayer
    compile("com.google.android.exoplayer:library-core:r2.99.1")
    compile("com.google.android.exoplayer:exoplayer-dash:$rootProject.ext.exoPlayerVersion") {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-core'
    }
    compile("com.google.android.exoplayer:exoplayer-hls:$rootProject.ext.exoPlayerVersion") {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-core'
    }
    compile("com.google.android.exoplayer:exoplayer-smoothstreaming:$rootProject.ext.exoPlayerVersion") {
        exclude group: 'com.google.android.exoplayer', module: 'exoplayer-core'
    }
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

// ======== MAVEN DEPLOY to local cache or nexus repo (to make accessible to local projects) (command: ./gradlew clean aRel uploadArchives) ========
uploadArchives {
    repositories {
        // deploy to local
        mavenDeployer {
            repository(url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = libraryGroupId
            pom.artifactId = libraryBaseName
            pom.version = libraryVersion
            //Normally I would append "-SNAPSHOT" but newer gradle/maven can't always find them
            pom.packaging = 'aar'
            uniqueVersion = false
            // don't put in a timestamp on the filename (other project will not be able to depend on it)
        }
    }
}

// ======= BINTRAY DEPLOY (command: $ ./gradlew clean build bintrayUpload -PbintrayUser=BINTRAY_USERNAME -PbintrayKey=BINTRAY_KEY -PdryRun=false) ====
publish {
    userOrg = 'ampme'
    groupId = libraryGroupId
    artifactId = libraryBaseName
    uploadName = 'ExoMedia'
    version = libraryVersion
    autoPublish = true
    dryRun = false
    bintrayUser = System.properties['bintray.user']
    bintrayKey = System.properties['bintray.key']
}
